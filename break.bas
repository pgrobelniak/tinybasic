1000 SW=640: SH=480
1005 BR=4: BC=10
1010 BW=SW/BC: T=20
1015 DIM BA(BR*BC)
1020 BO=3*T
1035 LS=T/4
1040 PS=LS*1.5: PW=100: PY=SH-T
1045 CLS
1050 GOSUB 3000
1500 SET 10,1
1505 COLOR 0,255,0
1510 FRECT 0,0,SW,SH
1520 COLOR 0,0,0
1525 GOSUB 2000
1550 SET 10,0
1555 DELAY 20
1595 GOTO 1500
2000 LX=LX+LU: LY=LY+LV
2005 HL=0: HR=0
2105 FOR R=1 TO BR
2110 FOR C=1 TO BC
2115 I=(R-1)*BC+C
2120 IF BA(I)=1 THEN  GOSUB 4000
2125 NEXT C
2130 NEXT R
2205 IF INKEY(80) THEN PX=PX-PS
2210 IF INKEY(79) THEN PX=PX+PS
2220 IF PX<0 THEN PX=0
2225 IF PX+PW=>SW THEN PX=SW-PW
2235 IF LY+T>PY AND LX<PX+PW AND LX+T>PX THEN LY=PY-T: LV=-LS: LU=(LX+T/2-PX-PW/2)/(PW/2)*LS
2240 COLOR 255,255,0
2250 FRECT PX,PY,PX+PW,PY+T
2300 IF HL=1 AND HR=1 THEN LY=HY: LV=-LV:  GOTO 2310
2305 IF HL=1 OR HR=1 THEN LX=HX: LV=-LV
2310 IF LX<0 THEN LX=0: LU=-LU
2315 IF LX+T=>SW THEN LX=SW-T: LU=-LU
2320 IF LY<0 THEN LY=0: LV=-LV
2325 IF LY+T=>SH THEN  GOSUB 3000
2328 COLOR 255,0,0
2330 FCIRCLE LX+T/2,LY+T/2,T/2
2995 RETURN
3000 PX=(SW-PW)/2: PV=0
3005 LX=(SW-T)/2: LY=PY-T: 
3010 LU=LS: LV=-LS
3100 FOR R=1 TO BR
3105 FOR C=1 TO BC
3110 I=(R-1)*BC+C
3115 BA(I)=1
3120 NEXT C
3125 NEXT R
3995 RETURN
4000 X=(C-1)*BW: Y=(R-1)*T+BO
4005 HB=0:  GOSUB 5000
4010 IF HB=1 THEN BA(I)=0: RETURN
4015 FRECT X+1,Y+1,X+BW-2,Y+T-2
4020 RETURN
5000 IF LX+T<X OR LX>X+BW OR LY+T<Y OR LY>Y+T THEN RETURN
5005 HB=1
5010 IF LX<X THEN HL=1: HX=X-T: RETURN
5015 IF LX>X+BW THEN HR=1: HX=X+BW: RETURN
5020 HL=1: HR=1
5025 IF LY<Y THEN HY=Y-T ELSE HY=Y+T
5995 RETURN
