10 REM "ANIMAL"
20 REM "Ported by Stefan in 2022"
30 REM
40 PRINT "PLAY 'GUESS THE ANIMAL'"
45 PRINT
50 PRINT "THINK OF AN ANIMAL AND THE COMPUTER WILL TRY TO GUESS IT."
60 PRINT
65 SET 12, 0: REM "SET the base of arrays to 0"
66 SET 11, 1: REM "SET the TAB command to MS mode"
70 DIM A$(32, 201)
80 FOR I=0 TO 3
90 READ A$()(I)
100 NEXT I
110 N=VAL(A$()(0))
120 REM "MAIN CONTROL SECTION"
130 INPUT "ARE YOU THINKING OF AN ANIMAL? ";AN$
140 IF AN$="LIST" THEN 600
143 IF AN$="SAVE" THEN 700
145 IF AN$="LOAD" THEN 800
147 IF AN$="END" THEN END
150 IF AN$(1,1)<>"Y" AND AN$(1,1)<>"y" THEN 120
160 K=1
170 GOSUB 390
180 IF LEN(A$()(K))=0 THEN 999
190 IF A$(1,2)(K)="\Q" THEN 170
200 PRINT "IS IT A ";A$(3)(K);
210 INPUT AN$
220 AN$=AN$(1,1)
230 IF AN$(1,1)="Y" THEN PRINT "WHY NOT TRY ANOTHER ANIMAL?": GOTO 120
240 INPUT "THE ANIMAL YOU WERE THINKING OF WAS A? ";V$
250 PRINT "PLEASE TYPE IN A QUESTION THAT WOULD DISTINGUISH A"
260 PRINT V$;" FROM A ";A$(3)(K)
270 INPUT X$
280 PRINT "FOR A ";V$;" THE ANSWER WOULD BE ";
290 INPUT AN$
300 AN$=AN$(1,1): IF AN$<>"Y" AND AN$<>"N" THEN 280
310 IF AN$="Y" THEN B$="N"
320 IF AN$="N" THEN B$="Y"
330 Z1=VAL(A$()(0))
340 A$()(0)=STR(Z1+2)
350 A$()(Z1)=A$()(K)
360 A$()(Z1+1)="\A": A$(3)(Z1+1)=V$
370 A$()(K)="\Q": A$(3)(K)=X$: A$(LEN(A$()(K))+1)(K)="\": A$(LEN(A$()(K))+1)(K)=AN$
375 A$(LEN(A$()(K))+1)(K)=STR(Z1+1): A$(LEN(A$()(K))+1)(K)="\": A$(LEN(A$()(K))+1)(K)=B$
378 A$(LEN(A$()(K))+1)(K)=STR(Z1): A$(LEN(A$()(K))+1)(K)="\"
380 GOTO 120
390 REM "SUBROUTINE TO PRINT QUESTIONS"
400 Q$=A$()(K)
410 FOR Z=3 TO LEN(Q$)
415 IF Q$(Z,Z)<>"\" THEN PRINT Q$(Z,Z);: NEXT Z
420 INPUT C$
430 C$=C$(1,1)
440 IF C$<>"Y" AND C$<>"N" THEN 410
450 T$="\": T$(2)=C$
455 FOR X=3 TO LEN(Q$)-1
460 IF Q$(X,X+1)=T$ THEN 480
470 NEXT X
475 STOP
480 FOR Y=X+1 TO LEN(Q$)
490 IF Q$(Y,Y)="\" THEN 510
500 NEXT Y
505 STOP
510 K=VAL(Q$(X+2,Y))
520 RETURN
530 DATA "4","\QDOES IT SWIM\Y2\N3\","\AFISH","\ABIRD"
600 PRINT:PRINT "ANIMALS I ALREADY KNOW ARE:"
605 X=0
610 FOR I=1 TO 200
620 IF A$(1,2)(I)<>"\A" THEN 650
624 TAB 15*X
630 FOR Z=3 TO LEN(A$()(I))
640 IF A$(Z,Z)(I)<>"\" THEN PRINT A$(Z,Z)(I);: NEXT Z
645 X=X+1: IF X=4 THEN X=0: PRINT
650 NEXT I
660 PRINT
670 PRINT
680 GOTO 120
700 REM "Save the data"
710 @S=0
720 OPEN &16, "animals.dat", 1
730 T1=VAL(A$()(0))
740 FOR I=0 TO T1-1: PRINT &16, A$()(I): NEXT I
750 CLOSE &16, 1
760 IF @S THEN PRINT "Save failed" ELSE PRINT "Save successfull"
770 @S=0
790 GOTO 120
800 REM "Load the DATA"
810 @S=0
820 OPEN &16, "animals.dat": IF @S THEN PRINT "File does not exist": GOTO 120
830 INPUT &16, T$: T1=VAL(T$): IF @S THEN PRINT "Invalid data": GOTO 120
840 A$()(0)=STR(T1)
850 FOR I=1 TO T1
860 INPUT &16, T$: A$()(I)=T$
870 NEXT
890 GOTO 120
999 END
